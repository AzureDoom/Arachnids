buildscript {
    repositories {
    	maven { url  = 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.0"
    }
}

plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput
import com.modrinth.minotaur.dependencies.ModDependency

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven { url 'https://dl.cloudsmith.io/public/azuredoom-mods/azurelib/maven/' }
    maven { url 'https://dl.cloudsmith.io/public/tslat/sbl/maven/' }
    maven { url 'https://plugins.gradle.org/m2/' }
	maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://api.modrinth.com/maven" }
    maven { url "https://maven.terraformersmc.com" }
    maven { url "https://maven.parchmentmc.org" }
	maven { url 'https://api.modrinth.com/maven' }
}

configurations {
    shade
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.19.3:2022.12.18@zip")
    }
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabricVersion}"
  	modImplementation 'mod.azure.azurelib:azurelib-fabric-1.19.4:1.0.8'
	modImplementation "maven.modrinth:midnightlib:1.3.0-fabric"
    shade group: 'maven.modrinth', name: 'midnightlib', version: '1.3.0-fabric'
	modImplementation ("com.terraformersmc:modmenu:6.1.0-rc.2")
	modImplementation "net.tslat.smartbrainlib:SmartBrainLib-fabric-1.19.4:1.9"
	modImplementation "net.tslat.smartbrainlib:SmartBrainLib-common-1.19.4:1.9"
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:11.0.597") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:11.0.597") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("dev.architectury:architectury-fabric:8.1.73") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("me.shedaniel.cloth:cloth-config-fabric:10.0.96") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    from sourceSets.main.output
    duplicatesStrategy = 'exclude'
    relocate 'eu.midnightdust', 'mod.azure.arachnids.shadowed.midnightlib'
    classifier 'dev'
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

artifacts {
    archives shadowJar
}

remapJar {
    dependsOn(shadowJar)
    inputFile = shadowJar.archiveFile
}

loom {
	accessWidenerPath = file("src/main/resources/arachnids.accesswidener")
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
	
	}
}


if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "${curse_id}"
        	releaseType = "${curse_release}"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
			addGameVersion("1.19.4")
			addGameVersion("Fabric")
			addGameVersion("Quilt")
			addGameVersion("Java 17")

        	relations {
            	requiredDependency 'fabric-api'  
            	requiredDependency 'azurelib' 
            	requiredDependency 'smartbrainlib' 
        	}
        	mainArtifact(remapJar)

        	afterEvaluate {
            	uploadTask.dependsOn("remapJar")
        	}
    	}
    	options {
    		//debug = true
        	forgeGradleIntegration = false
    	}
    }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())
		
		token = modrinthProp.getProperty('modrinthKey')
		projectId = 'AOs8oaMT' 
		versionNumber = project.mod_version 
		versionName = 'Fabric 1.19.4'
		uploadFile = remapJar 
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ['1.19.4']
		loaders = ['fabric','quilt']
		dependencies { 
        	required.project "azurelib"
        	required.project "smartbrainlib"
        	required.project "fabric-api"
            optional.project "modmenu"
            optional.project "rei"
   		}
	}
}